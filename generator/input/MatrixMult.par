Script MatrixManager{
    on getResult(int[] result, int index, RowCalculator sender);
}

main(){
    int [5][5] matrix1 = {{1, 2, 3, 4, 5}, {6, 7, 8, 9, 10}, {11, 12, 13, 14, 15}, {16, 17, 18, 19, 20}, {21, 22, 23, 24, 25}};
    int [5][5] matrix2 = {{1, 2, 3, 4, 5}, {6, 7, 8, 9, 10}, {11, 12, 13, 14, 15}, {16, 17, 18, 19, 20}, {21, 22, 23, 24, 25}};
    MatrixPrinter printer = Spawn MatrixPrinter();
    MatrixCalculator matrixCalc = Spawn MatrixCalculator(matrix1, matrix2, printer);
}

Actor MatrixCalculator follows MatrixManager{
    State{
        int[5][5] resultMatrix;
        int resultsReceived = 0;
    }
    Knows{MatrixPrinter printer;}
    Spawn(int[][] matrix1, int[][] matrix2, MatrixPrinter printer){
        Knows.printer = printer;
        for(int i = 0; i < 5; i = i +1){
            RowCalculator rowCalc = Spawn RowCalculator(self);
            int [5] row;
            for (int j = 0; j < 5; j = j+1){
                row[j] = matrix1[i][j];
            }
            rowCalc <- calculateRow(row, matrix2, i);
        }
    }

    on getResult(int[] result, int index, RowCalculator sender){
        for(int i = 0; i < 5; i = i+1){
            State.resultMatrix[index][i] = result[i];
        }
        sender <- kill();
        if (State.resultsReceived == 4){
            Knows.printer <- printMatrix(State.resultMatrix);
            KILL;
        }
        State.resultsReceived = State.resultsReceived + 1;
    }
}

Actor RowCalculator{
    State{
        int[5] resultRow;
    }
    Knows{MatrixManager parent;}
    Spawn(MatrixManager parent){
        Knows.parent = parent;
    }

    on calculateRow(int[] row, int[][] matrix, int index){
        for(int i = 0; i < 5; i = i+1){
            State.resultRow[i] = 0;
            for(int j = 0; j < 5; j = j+1){
                State.resultRow[i] = State.resultRow[i] + row[j] * matrix[i][j];
            }
        }
        Knows.parent <- getResult(State.resultRow, index, self);
    }

    on kill(){
        KILL;
    }
}

Actor MatrixPrinter{
    State{}
    Knows{}
    Spawn(){}

    on printMatrix(int[][] matrix){
        print(matrix);
        KILL;
    }
}